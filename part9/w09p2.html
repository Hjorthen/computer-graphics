<!DOCTYPE html>
<html><body>
        <div style="display: flex;">
            <div style="width: 600px">
                <canvas id="c" width="600px" height="200px"></canvas>
            </div>
        </div>
        <select id="perspective" size="1">
            <option value="0">Orthographic</option>
            <option value="1">Perspective</option>
        </select>
        <div>
            <input id="rotate" type="checkbox">Rotate</input>
            <input id="rotation" type="range" min="0" max="360" value="0">
        </div>
        <div>
            <input id="Move" type="checkbox">Height</input>
            <input id="Height" type="range" min="-1" max="0.5" step="0.1" value="0">
        </div>
        <script id="vertex-shader-teacup" type="x-shader/x-vertex">
            attribute vec4 a_Position;
            attribute vec4 a_Normal;
 
            uniform mat4 P;
            uniform mat4 M;
            uniform mat4 V;
            uniform vec4 lightPosition;
            uniform mat3 normMatrix;
            uniform mat4 lightMVP;


            varying vec3 cameraNorm, lightDir, eyeDir;
            varying vec4 lightPerspectivePosition;

            void main()
            {
                mat4 MV = V * M;
                gl_Position = P * MV *  a_Position;
                lightPerspectivePosition = lightMVP * a_Position;
                 
                vec3 posInCameraSpace = (MV * a_Position).xyz;
                eyeDir = normalize(-posInCameraSpace);
                lightDir = normalize(lightPosition.w == 0.0 ? normalize(lightPosition.xyz) : (V * lightPosition).xyz  - posInCameraSpace);
                cameraNorm = normalize(normMatrix * a_Normal.xyz);
            }
        </script>
        <script id="fragment-shader-teacup" type="x-shader/x-fragment">
            precision mediump float;

            varying vec3 cameraNorm, lightDir, eyeDir;
            varying vec4 lightPerspectivePosition;

            uniform vec3 ambience;
            uniform vec3 diffuse;
            uniform vec3 specular;
            uniform float shininess;
            uniform sampler2D shadowMap;
            void main()
            {
                // Calculate Phong shading 
                vec3 H = normalize(lightDir + eyeDir);
                float diffuseFactor = max(dot(normalize(lightDir), normalize(cameraNorm)), 0.0);
                float specularFactor = pow(max(dot(normalize(cameraNorm), H), 0.0), shininess);
                
                if(dot(normalize(lightDir), normalize(cameraNorm)) < 0.0)
                    specularFactor = 0.0;
                
                vec3 fragPos = 0.5*(lightPerspectivePosition.xyz / lightPerspectivePosition.w) + 0.5;
                float shadowDepth = texture2D(shadowMap, fragPos.xy).r;
                float shaderContrib = 1.0;
                if(shadowDepth + 0.002 < fragPos.z)
                    shaderContrib = 0.2;
 
                gl_FragColor = vec4(diffuse* diffuseFactor + (specular* specularFactor + ambience) * shaderContrib, 1);
            }
        </script>
        <script id="vertex-shader" type="x-shader/x-vertex">
            attribute vec4 a_Position;
            attribute vec2 a_UV;            
 
            uniform mat4 P;
            uniform mat4 MV;
            uniform mat4 lightMVP;
            varying vec2 uv;
            varying vec4 lightPerspectivePosition;

            void main()
            {
                lightPerspectivePosition = lightMVP * a_Position;
                gl_Position = P * MV *  a_Position;
                uv = a_UV;
            }
        </script>
        <script id="fragment-shader" type="x-shader/x-fragment">
            precision mediump float;
            varying vec2 uv;
            uniform sampler2D colorTexture;
            varying vec4 lightPerspectivePosition;
            uniform sampler2D shadowMap;
            void main()
            {
                vec3 fragPos = 0.5*(lightPerspectivePosition.xyz / lightPerspectivePosition.w) + 0.5;
                float shadowDepth = texture2D(shadowMap, fragPos.xy).r;
                float shadowContrib = 1.0;
                if(shadowDepth + 0.005 < fragPos.z)
                    shadowContrib = 0.2;
                gl_FragColor = vec4(texture2D(colorTexture, uv).xyz * shadowContrib, 1);
            }
        </script>
        <script id="fragment-shader-shadowmap" type="x-shader/x-fragment">
            precision mediump float;
            void main()
            {
                gl_FragColor = vec4(gl_FragCoord.z, 0.0, 0.0, 1.0);
            }
        </script>
        <script id="vertex-shader-shadowmap" type="x-shader/x-vertex">
            attribute vec4 a_Position;
            uniform mat4 MVP;

            void main()
            {
                gl_Position = MVP *  a_Position;
            }
        </script>
        <script type="text/javascript" src="Light.js"></script>
        <script type="text/javascript" src="initShaders.js"></script>
        <script type="text/javascript" src="Primitives.js"></script>
        <script type="text/javascript" src="DrawArray.js"></script>
        <script type="text/javascript" src="MV.js"></script>
        <script type="text/javascript" src="OBJParser.js"></script>
        <script type="text/javascript" src="angel\webgl-utils.js"></script>
        <script type="text/javascript" src="GLBuffer.js"></script>
        <script type="text/javascript" src="code09p2.js"></script>
</body></html>
