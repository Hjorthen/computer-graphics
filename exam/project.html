<!DOCTYPE html>
<html><body>
        <div style="display: flex;">
            <div style="width: 600px">
                <canvas id="c" width="600px" height="300px"></canvas>
            </div>
        </div>
        <div>
            Noise:
            <input type="range" min="0" step="0.25" max="5" value="1" id="noise">
            Smooth:
            <input type="range" min="0.0" step="0.05" max="1.0" value="1.0" id="smoothness">
        </div>
        <div>
            Rotation:
            <input type="range" min="1" step="10" value="0" max="360" value="51" id="rotation">
            Rotate <input type="checkbox" id="rotate" checked>
        </div>
        <script id="vertex-shader-terrain" type="x-shader/x-vertex">
        const float lacunarity = 2.0;
        const int octaves = 7;
         
        // perlin simplex from: https://github.com/stegu/webgl-noise/blob/master/src/noise2D.glsl
        vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec2 mod289(vec2 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec3 permute(vec3 x) {
        return mod289(((x*34.0)+1.0)*x);
        }

        float snoise(vec2 v)
        {
        const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                          0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                         -0.577350269189626,  // -1.0 + 2.0 * C.x
                          0.024390243902439); // 1.0 / 41.0
        // First corner
        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);

        // Other corners
        vec2 i1;
        //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
        //i1.y = 1.0 - i1.x;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        // x0 = x0 - 0.0 + 0.0 * C.xx ;
        // x1 = x0 - i1 + 1.0 * C.xx ;
        // x2 = x0 - 1.0 + 2.0 * C.xx ;
        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;

        // Permutations
        i = mod289(i); // Avoid truncation effects in permutation
        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));

        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;

        // Gradients: 41 points uniformly over a line, mapped onto a diamond.
        // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;

        // Normalise gradients implicitly by scaling m
        // Approximation of: m *= inversesqrt( a0*a0 + h*h );
        m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

        // Compute final noise value at P
        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
        }
        // End Perlin
        float pnoisenorm(vec2 point)
        {
            return snoise(point) * 0.5 + 0.5; 
        }
        float fbm(vec2 point, float H)
        {
            float result = 0.0;
            float max = 0.0;
            for(int i=0;i<octaves;++i)
            {
                float dampen = pow(lacunarity, -H * float(i));
                result += pnoisenorm(point) * dampen;
                max += 1.0 * dampen;
                point *= lacunarity;
            }
            return  result / max;
        }

        attribute vec4 a_Position;


        uniform float scale;
        uniform float H;
        uniform mat4 P;
        uniform mat4 MV;
        uniform mat3 normMatrix;
        uniform mat4 lightMVP;
        uniform mat3 shadowMV;

        varying float height;
        varying float heightNoise;
        varying float textureNoise;
        varying vec4 lightPerspectivePosition;
        // For diffuse shading
        varying vec3 normal;
         
        vec3 estimateNormal(vec3 position)
        {
            return normalize(vec3(
            fbm(scale * vec2(position.x - 0.01, position.y), H) - fbm(scale * vec2(position.x + 0.01, position.y), H), 
            fbm(scale * vec2(position.x, position.y - 0.01), H) - fbm(scale * vec2(position.x, position.y + 0.01), H), 
                    2.0 * position.z)); 
        }

        void main()
        {
            const bool use_fbm = true;
            float delta = use_fbm ? fbm(scale * a_Position.xy, H) : pnoisenorm(scale * a_Position.xy);
            height = delta;
            heightNoise = pnoisenorm(20.0 * a_Position.xy) * 0.05;
            textureNoise = pnoisenorm(64.0 * a_Position.xy) * 0.5 + 0.5;
            vec4 transformedPosition = vec4(a_Position.xy, delta, 1);
            lightPerspectivePosition = lightMVP * transformedPosition;
            gl_Position = P * MV * transformedPosition;

            normal = estimateNormal(a_Position.xyz);
        }
        </script>
        <script id="fragment-shader-terrain" type="x-shader/x-fragment">
            precision mediump float;
            const vec3 snow = vec3(203, 214, 206)/255.0;        
            const vec3 ground = vec3(80, 53, 37)/255.0;
            const vec3 grass = vec3(56, 112, 0)/255.0;

            uniform sampler2D shadowMap;
            float unpackDepth(const in vec4 rgbaDepth) {   
            const vec4 bitShift = vec4(1.0, 1.0/256.0, 
                                       1.0/(256.0*256.0), 
                                       1.0/(256.0*256.0*256.0));   
            return dot(rgbaDepth, bitShift); 
            } 
            varying float height;
            varying float heightNoise;
            varying float textureNoise;
            varying vec4 lightPerspectivePosition;
            // Diffuse shading
            varying vec3 normal;
            uniform vec3 lightPosition;
            const vec2 texmapScale = vec2(1.0 / 512.0, 1.0 / 512.0);

            float offset_lookup(vec3 shadowCoord, vec2 offset)
            {
                return unpackDepth(texture2D(shadowMap, shadowCoord.xy + offset * texmapScale));
            }
        

            void main()
            {
                vec3 color;
                if((height + abs(heightNoise)) > 0.66)
                    color = snow * textureNoise;
                else if(height + abs(heightNoise) > 0.4)
                    color = ground * ((textureNoise - 0.5) * 0.5 + 0.75); 
                else 
                    color = grass * textureNoise;

                vec3 lightNDC = 0.5 * (lightPerspectivePosition.xyz / lightPerspectivePosition.w) + 0.5;
                float sum = 0.0;
                for(float y=-1.5;y<=1.5;y+=1.0)
                {
                    for(float x=-1.5;x<=1.5;x+=1.0)
                    {
                        sum += offset_lookup(lightNDC, vec2(x, y));
                    }
                }

                float shadowDepth = sum / 16.0;
                //shadowDepth = unpackDepth(texture2D(shadowMap, lightNDC.xy));
                float shaderContrib = 1.0;
                if(shadowDepth + 0.0010 < lightNDC.z)
                    shaderContrib = 0.2;
                

                // Diffuse
                float diffuseFactor = max(dot(normalize(lightPosition), normalize(normal)), 0.0); 
                float t = dot(normalize(lightPosition), normalize(normal));
                vec3 diffuse = 0.9 * color * diffuseFactor;
                vec3 ambient = color * 0.8;
                gl_FragColor = vec4((diffuse + ambient)  * shaderContrib, 1); 
             }
        </script>
        <script id="vertex-shader-shadowmap" type="x-shader/x-fragment">
        const float lacunarity = 2.0;
        const int octaves = 7;
        // perlin simplex from: https://github.com/stegu/webgl-noise/blob/master/src/noise2D.glsl
        vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec2 mod289(vec2 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec3 permute(vec3 x) {
        return mod289(((x*34.0)+1.0)*x);
        }

        float snoise(vec2 v)
        {
        const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                          0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                         -0.577350269189626,  // -1.0 + 2.0 * C.x
                          0.024390243902439); // 1.0 / 41.0
        // First corner
        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);

        // Other corners
        vec2 i1;
        //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
        //i1.y = 1.0 - i1.x;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        // x0 = x0 - 0.0 + 0.0 * C.xx ;
        // x1 = x0 - i1 + 1.0 * C.xx ;
        // x2 = x0 - 1.0 + 2.0 * C.xx ;
        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;

        // Permutations
        i = mod289(i); // Avoid truncation effects in permutation
        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));

        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;

        // Gradients: 41 points uniformly over a line, mapped onto a diamond.
        // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;

        // Normalise gradients implicitly by scaling m
        // Approximation of: m *= inversesqrt( a0*a0 + h*h );
        m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

        // Compute final noise value at P
        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
        }
        // End Perlin
 
        float pnoisenorm(vec2 point)
        {
            return snoise(point) * 0.5 + 0.5; 
        }
        float fbm(vec2 point, float H)
        {
            float result = 0.0;
            float max = 0.0;
            for(int i=0;i<octaves;++i)
            {
                float dampen = pow(lacunarity, -H * float(i));
                result += pnoisenorm(point) * dampen;
                max += 1.0 * dampen;
                point *= lacunarity;
            }
            return  result / max;
        }

        attribute vec4 a_Position;
        uniform float scale;
        uniform float H;
        uniform mat4 P;
        uniform mat4 MV;
        varying float height;
        void main()
        {
            const bool use_fbm = true;
            float delta = use_fbm ? fbm(scale * a_Position.xy, H) : pnoisenorm(scale * a_Position.xy);
            height = delta;
            vec4 transformedPosition = vec4(a_Position.xy, delta, 1);
            gl_Position = P * MV * transformedPosition;
        }
        </script>
        <script id="fragment-shader-shadowmap" type="x-shader/x-fragment">
            precision mediump float;
            varying float height;
            void main()
            {
                const vec4 bitShift = vec4(1.0, 256.0, 256.0*256.0, 256.0*256.0*256.0); 
                const vec4 bitMask = vec4(1.0/256.0, 1.0/256.0, 1.0/256.0, 0.0);
                vec4 rgbaDepth = fract(gl_FragCoord.z*bitShift);
                rgbaDepth -= rgbaDepth.gbaa*bitMask;
                gl_FragColor = rgbaDepth;//vec4(gl_FragCoord.z, 0, 0, 1.0);
            }
        </script>
        <script type="text/javascript" src="Light.js"></script>
        <script type="text/javascript" src="../lib/angel/initShaders.js"></script>
        <script type="text/javascript" src="Primitives.js"></script>
        <script type="text/javascript" src="DrawArray.js"></script>
        <script type="text/javascript" src="../lib/angel/MV.js"></script>
        <script type="text/javascript" src="OBJParser.js"></script>
        <script type="text/javascript" src="../lib/angel/webgl-utils.js"></script>
        <script type="text/javascript" src="GLBuffer.js"></script>
        <script type="text/javascript" src="project.js"></script>
</body></html>
